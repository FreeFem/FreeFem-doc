// Parameters
real al=0.5;
real dt = 0.05;
// Mesh
border C(t=0., 2.*pi) {x=cos(t); y=sin(t);};
mesh Th = buildmesh(C(100));

// Fespace
fespace Vh(Th,P1dc);
Vh w, ccold, v1 = y, v2 = -x, cc = exp(-10*((x-0.3)^2 +(y-0.3)^2));

// Macro
macro n() (N.x*v1 + N.y*v2) // Macro without parameter

// Problem
problem  Adual(cc, w)
	= int2d(Th)(
		  (cc/dt+(v1*dx(cc)+v2*dy(cc)))*w
	)
	+ intalledges(Th)(
		  (1-nTonEdge)*w*(al*abs(n)-n/2)*jump(cc)
	)
	- int2d(Th)(
		  ccold*w/dt
	)
	;

// Time iterations
for (real t = 0.; t < 2.*pi; t += dt){
	ccold = cc;
	Adual;
	plot(cc, fill=1, cmm="t="+t+", min="+cc[].min+", max="+ cc[].max);
}

real [int] viso = [-0.2, -0.1, 0., 0.1, 0.2, 0.3, 0.4, 0.5, 0.6, 0.7, 0.8, 0.9, 1., 1.1];
plot(cc, wait=1, fill=1, ps="ConvectCG.eps", viso=viso);
plot(cc, wait=1, fill=1, ps="ConvectDG.eps", viso=viso);



